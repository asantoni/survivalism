//A deployed 3D door
actor CDoor : Actor
{
	Tag "Door"
	+SOLID
	+SHOOTABLE
	Speed 0
	+NOBLOOD
	//+NOGRAVITY
	//Activation THINGSPEC_Default
	+CANPASS //---> crazy CPU usage
    +USESPECIAL
    //+AMBUSH
    +QUICKTORETALIATE
	+DONTRIP
    
    
	Health 1100
	Radius 24
	Height 76
	Mass 0x7FFFFFFF
	
	DamageFactor "Axe", 10.0 //Takes a few hits with the axe to destroy
	DamageFactor "SentryTurret", 0.0
	
	States
	{
		Spawn:
			TNT1 A 0 //Dummy frame is needed for A_Jump to work
			//Set the TID to CONSTRUCTION_TID so that monsters can
			//attack it (baddies.acs)
			TNT1 A 0 ACS_ExecuteAlways(503, 0)			
			CDOR A 1 Thing_SetSpecial(tid, 226, 935, tid, 0)
            CDOR A 0 
			goto Closed
		Active:
		Open:
			TNT1 A 0 //Dummy frame is needed for A_Jump to work
			//Don't actually open unless we have the player as our target, so
			//we can detect when they walk away.
			CDOR AAAAAAA 3 A_LookEx(0, 0, 64.0, 64.0, 360, "ReallyOpen")
			TNT1 A 0 A_PlaySound("click") //FIXME: Debugging
			Loop
		ReallyOpen:
			TNT1 A 0
			CDOR A 1 A_PlaySound("doors/dr1_open") //A_PlaySound("DSDOROPN")
			//TNT1 A 0 A_HideThing
			TNT1 A 0 A_UnSetShootable
			TNT1 A 0 A_UnsetSolid
            TNT1 A 0 A_SpawnItemEx("CDoorCeiling", 0, 0, 76.0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
			CDOR BCDEFGHI 1
			goto See
			/*
		CheckIfPlayerIsClose:
			TNT1 A 0 //Dummy frame
			CDOR A 8 A_LookEx(0, 0, 64.0, 64.0, 360, "See")
			Loop
			*/
		See: //If the player is right beside the door
		//TODO: (BUGFIX / AENIMA)
		//Set the activator here to be the target!
			//TNT1 A 0 A_Set
		WaitForPlayerToGoAway:
			CDOR J 0 //Dummy frame
			CDOR J 34 A_SpawnItemEx("CDoorCeiling", 0, 0, 76.0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
			CDOR J 1 A_JumpIfCloser(64.0, "WaitForPlayerToGoAway")
			goto Closed
		Inactive:
		Closed:
			//CDOR A 35 A_PlaySound("creeper/fuse")
			TNT1 A 0 //Dummy frame
			TNT1 A 0 A_PlaySound("doors/dr1_clos")
			TNT1 A 0 A_UnHideThing
			TNT1 A 0 A_SetShootable
			TNT1 A 0 A_SetSolid
			TNT1 A 0 A_ClearTarget
			CDOR IHGFEDCB 1
			CDOR A -1 
			Wait
		Death:
			TNT1 A 0 //Dummy frame is needed
			TNT1 A 0 A_SpawnItemEx("CDoorItem", 8, 0, 8, 0, 0, 0, 0, 0, 0)
            TNT1 A 0 A_RemoveChildren(true)
			Stop
	}
}

/** 
 *  An invisible block that sits above doors, which prevents blocks from falling into your door when you open it.
 */
actor CDoorCeiling
{
	+SOLID
	Speed 0
	+NOBLOOD
	+NOGRAVITY
	+CANPASS //---> crazy CPU usage
	+DONTRIP
	
	Health 0x7FFFFFFF
	Radius 24
	Height 0
	Mass 0x7FFFFFFF
	
	//DamageFactor "Axe", 10.0 //Takes a few hits with the axe to destroy
	//DamageFactor "SentryTurret", 0.0
	
    States
    {
        Spawn:
            TNT1 A 0 
            TNT1 A 35 //Invisible block
            Stop
    }
    
}

//The construction box item you pick up off the ground.
actor CDoorItem : CustomInventory 1155
{
	Tag "Door"
	+SHOOTABLE
	+NOBLOOD
	//+CANPASS ---> crazy CPU usage
	+WINDTHRUST
	Health 50
	Inventory.PickupMessage "Picked up a construction door"
	Inventory.UseSound "misc/invuse"
	+INVENTORY.INVBAR
	Inventory.Icon "CDRIA0" //"PISTI0"
	Inventory.MaxAmount 400
	//+COUNTITEM
	+INVENTORY.PICKUPFLASH
	
   States {
      Spawn:
         CDRG A 1
         Wait
      Use:
         TNT1 A 0 A_JumpIfInventory("ConstructionMutex",1,"Cancel")
         TNT1 A 0 A_SpawnItemEx("CDoor", -x%48+((x%48)>24)*48-((x%48)<-24)*48, -y%48+((y%48)>24)*48-((y%48)<-24)*48, 256.0, 0.0, 0.0, 0.0, 0.0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEMOMENTUM)
         TNT1 A 0 ACS_ExecuteAlways(256,0)      //Give ConstructionMutex, Wait, Take ConstructionMutex
         Stop
      Cancel:
         TNT1 A 0
         Fail
   }

}


actor CDoorGhost
{
	Tag "Construction Door Ghost"
	-SOLID
	//+CANPASS ---> crazy CPU usage
    +USESPECIAL
	+NOGRAVITY	
	
	Health 0x7FFFFFFF
	Radius 24
	Height 34
	Mass 0x7FFFFFFF
		
	States
	{
		Spawn:
			TNT1 A 0 //Dummy frame is needed for A_Jump to work
			//PLAY A 200 A_Jump(256, "DropYellowboy")
			//TNT1 A 0 A_SetSpecial(80, 
			CDOR A 4 
			goto Death
		Death:
			TNT1 A 0 //Dummy frame is needed
			TNT1 A -1
			Stop

	}
}