
//Dummy item
Actor ShowFurnaceScreen : Inventory {}


//The furnace box item you pick up off the ground.
actor FurnaceItem : CustomInventory
{
	Tag "Furnace"
	+SHOOTABLE
	+NOBLOOD
    +WINDTHRUST
	
	//+CANPASS ---> crazy CPU usage
	Health 50
	Inventory.PickupMessage "Picked up a furnace."
	Inventory.UseSound "misc/invuse"
	+INVENTORY.INVBAR
	Inventory.Icon "FURNY" //"PISTI0"
	Inventory.MaxAmount 400
	//+COUNTITEM
	+INVENTORY.PICKUPFLASH
	
	DamageFactor "SentryTurret", 0.0
	
   States {
      Spawn:
         FURN A 1
         Wait
      Use:
         TNT1 A 0 A_JumpIfInventory("ConstructionMutex",1,"Cancel")
         TNT1 A 0 A_SpawnItemEx("Furnace", -x%48+((x%48)>24)*48-((x%48)<-24)*48, -y%48+((y%48)>24)*48-((y%48)<-24)*48, 256.0, 0.0, 0.0, 0.0, -angle%90+((angle%90)>45)*90+180, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEMOMENTUM)
         TNT1 A 0 ACS_ExecuteAlways(256,0)      //Give ConstructionMutex, Wait, Take ConstructionMutex
         Stop
      Cancel:
         TNT1 A 0
         Fail
   }
}
//Dummy item
actor FurnaceFuel : Inventory 
{ 
	Inventory.MaxAmount 3600
}
actor FurnaceTidCounter : Inventory {}

// A deployed furnace
Actor Furnace : Actor //: SwitchableDecoration 
{
	Tag "Furnace"
	+SHOOTABLE
	+NOBLOOD
	+SOLID
	+CANPASS
    +USESPECIAL
	+FRIENDLY
	+ISMONSTER // Needed for master/child relationship to work

	Health 200
	Radius 24
	Height 38
	Mass 0x7FFFFFFF
    
    ActiveSound "vile/firecrkl"
	
	DamageFactor "Axe", 100.0
//SetActivatorToTarget
	States
	{
		Spawn:
			TNT1 A 0 //Dummy frame is needed for A_Jump to work
			TNT1 A 0 A_SpawnItemEx("MonsterSpawnerRepeller", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER, 0)
			TNT1 A 0 A_PlaySound("click")
			TNT1 A 0 Thing_SetSpecial(0, 226, 770, 0, 0)
			TNT1 A 0 ACS_ExecuteAlways(774, 0) //Assign it a unique TID
			goto Idle
			//TNT1 A 0 ACS_ExecuteAlways(503, 0)	

			//FURN A 1 Thing_SetSpecial(tid, acs_execute, 770, tid, 0)
		Idle:
			TNT1 A 0
			TNT1 A 0 A_JumpIfInventory("FurnaceFuel", 1, "Cook")
			FURN A 35
			Loop
		Cook:
			TNT1 A 0
			//About 1 seconds of burn for 1 FurnaceFuel
			FURN BCDEFGH 10
			TNT1 A 0 A_TakeInventory("FurnaceFuel", 1)
            TNT1 A 0 A_ActiveSound
			FURN EFG 10
			FURN H 10
			goto Idle
		Death:
			TNT1 A 0 //Dummy frame is needed
			TNT1 A 0 A_KillChildren
			//Turn back into a CraftingBoxItem when we get destroyed
			TNT1 A 0 A_SpawnItemEx("FurnaceItem", 8, 0, 8, 0, 0, 0, 0, 0, 0)
			Stop
	}
}