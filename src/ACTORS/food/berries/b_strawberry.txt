ACTOR StrawberryPlant : SwitchingDecoration
{
	+SOLID
	+USESPECIAL
	+FLOORCLIP
	Activation THINGSPEC_ACTIVATE
	Radius 8
	Height 38
	Renderstyle Normal
	Alpha 0.0
	States
	{
	Spawn:
	BPLN CCCCCCCCCC 1 A_FadeIn(0.1)
	Resting:
	BPLN CCCCCC 300
	BPLN A 0 A_Jump(0,"ChangeToAnother")
	Loop
	
	ChangeToAnother:
	BPLN CCCCCCCCCC 1 A_FadeOut(0.1,false)
	Goto WaitForSpawn
	
	WaitForSpawn:
	TNT1 A 0
	TNT1 A 0 A_NoBlocking
	TNT1 A 0 A_UnsetSolid
	TNT1 A 0 A_SpawnItemEx("BerryRandomizer",0,0,0)
	TNT1 A 1
	Stop
	
	Active:
	BPLN A 0
	BPLN A 0
	Goto Harvest
	
	Harvest:
	BPLN A 0
	BPLN A 0 A_SpawnItemEx("StrawberryItem",random(-2,2),random(-2,2),random(5,25),random(-4,4),random(-4,4),random(5,12))
	BPLN A 0 A_Jump(64,"PostHarvest")
	BPLN D 1
	Loop
	
	PostHarvest:
	BPLN D 30
	BPLN DDDDDDDDDD 1 A_FadeOut(0.1, false)
	TNT1 A 0 A_SpawnItemEx("BerryRandomizer",0,0,0)
	Stop
	}
}

ACTOR StrawberryItem : BlueberryItem
{
  Tag "Strawberries"
  Inventory.MaxAmount 20
  Inventory.Icon "ZSEIB"
  Inventory.PickupMessage "Picked up a handful of red strawberries."
  Scale 1.0
  
  States
  {
	  Spawn:
	    CCHN A 0
		TNT1 A 0 ACS_ExecuteAlways(953, 0, 0) //Set our TID to FOOD_TID so we can spoil
		ZSEP B 10500 //Disappear after 5 minutes
		Stop
	  Use:
		TNT1 A 0
		TNT1 A 0 A_GiveInventory("Health", 4)
		TNT1 A 0 A_TakeInventory("HungerLevel", 4)
		TNT1 A 0 A_PlaySound("eat/crunch")
		TNT1 A 0 ACS_ExecuteAlways(850, 0, 0) //Green screen flash - PLAYER.ACS
		stop

  }
}