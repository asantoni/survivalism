Actor ImpWarlord
{
    Health 250
    Radius 20
    Height 56
    Speed 9
    PainChance 128
    Monster
    +FLOORCLIP
    SeeSound "imp/sight"
    PainSound "imp/pain"
    DeathSound "imp/death"
    ActiveSound "imp/active"
    Obituary "%o couldn't run from the imp warlord's fireball."
    HitObituary "%o was beaten to death by an imp warlord."
	DropItem "MediumHighLoot"
    States
    {
    Spawn:
        WARI AB 10 A_Look
        WARI AB 10 A_Look
        WARI AB 10 A_Look
        WARI AB 10 A_Look
        WARI AB 10 A_Look
        WARI AB 10 A_Look
        WARI AB 10 A_Look
        WARI AB 10 A_Look
        WARI AB 10 A_Look
        WARI A 0 A_ClearTarget
        WARI A 0 A_CheckSight("Remove")
        Loop
    See:
        WARI AABBCCDD 3 A_Chase
        WARI AABBCCDD 3 A_Chase
        WARI AABBCCDD 3 A_Chase
        WARI AABBCCDD 3 A_Chase
        WARI AABBCCDD 3 A_Chase
        WARI AABBCCDD 3 A_Chase
        WARI AABBCCDD 3 A_Chase
        WARI AABBCCDD 3 A_Chase
        WARI AABBCCDD 3 A_Chase
        WARI AABBCCDD 3 A_Chase
        WARI AABBCCDD 3 A_Chase
        WARI AABBCCDD 3 A_Chase
        WARI AABBCCDD 3 A_Chase
        WARI AABBCCDD 3 A_Chase
        WARI AABBCCDD 3 A_Chase
        WARI AABBCCDD 3 A_Chase
        WARI AABBCCDD 3 A_Chase
        WARI AABBCCDD 3 A_Chase
        WARI AABBCCDD 3 A_Chase
        WARI A 0 A_ClearTarget
        WARI A 0 A_CheckSight("Remove")
        Loop
    Melee:
        WARI E 6 A_FaceTarget
        TNT1 A 0 A_PlaySound("implord/maceswing")
        WARI F 4 A_FaceTarget
        WARI G 5 A_CustomMeleeAttack(Random(1,8)*7, "implord/macehit", "None")
        Goto See
    Missile:
        TNT1 A 0 A_Jump(38, 23)  //20% chance: jump to familiar drop
        TNT1 A 0 A_Jump(77, 2)   //30% chance: jump to heat seekers
        TNT1 A 0 A_Jump(128, 11) //50% chance: jump to green fireballs
        Goto Missile+28         //Otherwise : jump to red fireball
        TNT1 A 0 A_SetInvulnerable
        WARI I 6 A_FaceTarget
        TNT1 A 0 A_PlaySound("implord/shieldshot")
        WARI J 6 bright A_FaceTarget
        TNT1 A 0 A_Jump(128, 4)
        TNT1 A 0 A_CustomMissile("YellowSkullSeeker", 32, 0, -45)
        WARI K 6 bright A_CustomMissile("YellowSkullSeeker", 32, 0, 45)
        TNT1 A 0 A_UnSetInvulnerable
        Goto See
        WARI K 6 bright A_CustomMissile("YellowSkullSeeker", 32, 0, 0)
        TNT1 A 0 A_UnSetInvulnerable
        Goto See
        WARI L 7 A_FaceTarget
        TNT1 A 0 A_PlaySound("implord/maceswing")
        WARI M 5 A_FaceTarget
        TNT1 A 0 A_PlayWeaponSound("imp/attack")
        TNT1 A 0 A_Jump(128, 3)
        TNT1 A 0 A_CustomMissile("GreenPoisonBall", 40, 0, -8, 1)
        TNT1 A 0 A_CustomMissile("GreenPoisonBall", 24, 0, 8, 1)
		TNT1 A 0 A_CustomMissile("GreenPoisonBall", 32, 0, 0, 1)
        TNT1 A 0 A_CustomMissile("GreenPoisonBall", 36, 0, -4, 1)
        WARI N 6 A_CustomMissile("GreenPoisonBall", 28, 0, 4, 1)
        Goto See
		/* Disabling the familiars because they're annoying - Hax
        WARI GF 8 A_FaceTarget
        TNT1 A 0 A_Jump(128, 2)
        WARI E 8 A_CustomMissile("FamiliarSpawner", 32, 0, -30)
        Goto See
        WARI E 8 A_CustomMissile("FamiliarSpawner", 32, 0, 30)
        Goto See
		*/
        TNT1 A 0 A_Jump(128, 5)
        WARI O 7 A_FaceTarget
        TNT1 A 0 A_PlaySound("implord/maceswing")
        WARI P 5 A_FaceTarget
        WARI Q 6 A_CustomMissile("RedSkullBall", 32, 0, 0)
        WARI O 7 A_FaceTarget
        TNT1 A 0 A_PlaySound("implord/maceswing")
	    WARI P 5 A_FaceTarget
        WARI Q 6 A_CustomMissile("RedSkullBall", 32, 0, 0)
        Goto See
    Pain:
        TNT1 A 0 A_Jump(128, 2)
        WARI H 8 A_Pain
        Goto See
	    WARI I 1
        TNT1 A 0 A_SetInvulnerable
	    WARI IIIIII 4 A_FaceTarget
        TNT1 A 0 A_Jump(160, 5)
        WARI IIII 4 A_FaceTarget
        Goto Pain+4
        TNT1 A 0 A_UnSetInvulnerable
        Goto See
      Remove:
        TNT1 A 1 
        //Remove ourselves if we're too far from the player frustrum.
        TNT1 A 5 ACS_ExecuteAlways(501, 0)
        Stop
    Death:
		TNT1 A 0 A_GiveToTarget("ImpWarlordKillXP", 1)
    Death.NoXP:
    Death.SentryTurret:
        TNT1 A 0 A_FaceTarget
        TNT1 A 0 A_SpawnItemEx("WarlordMace", 0, 0, 32, 4, 0, 0, -90, 128)
        WARI R 8 A_SpawnItemEx("WarlordShield", 0, 0, 32, 5, 0, 0, 90, 128)
        WARI S 8 A_Scream
        WARI T 6
	    WARI U 6 A_Fall
		WARI V 2100
		TNT1 A 0 A_SetTranslucent(1.0, 2)
		WARI V 6 A_SetTranslucent(0.8, 2)
		WARI V 6 A_SetTranslucent(0.6, 2)
		WARI V 6 A_SetTranslucent(0.4, 2)
		WARI V 6 A_SetTranslucent(0.2, 2)
		WARI V 6 A_SetTranslucent(0.0, 0) 
		WARI V 0 ACS_ExecuteAlways(501, 0)
        Stop
    Raise:
        WARI VU 8
        WARI TSR 6
        Goto See
    }
}

Actor YellowSkullSeeker
{
    Radius 8
    Height 6
    Speed 9
    Damage 3
    +SEEKERMISSILE
	+SHOOTABLE
	Health 20
    Projectile
    RenderStyle Add
    Scale 0.67
    Alpha 0.67
    DeathSound "implord/shieldshothit"
    Decal RevenantScorch
    States
    {
    Spawn:
        WLP2 ABCD 3 Bright A_SeekerMissile(5, 10)
        Loop
    Death:
        WLP2 EFG 4 Bright
        Stop
    }
}

Actor GreenPoisonBall
{
    Radius 8
    Height 6
    Speed 10
    Damage 3
    Projectile
    RenderStyle Add
    Alpha 0.67
    DeathSound "imp/shotx"
	Decal HImpScorch
    States
    {
    Spawn:
        WLP1 ABCD 4
        Loop
    Death:
        WLP1 EFGH 4
        Stop
    }
}

Actor FamiliarSpawner
{
     +MISSILE
     +DOOMBOUNCE
     +BOUNCEONACTORS
	 +CANBOUNCEWATER
     +BLOODLESSIMPACT
     +PAINLESS
     Speed 8
     States
     {
     Spawn:
          WLP3 AAAABBBBCCCCDDDD 1 A_JumpIf(WaterLevel >= 2, "Death")
          Loop
     Death:
          TNT1 A 0 A_ChangeFlag(MISSILE, 0)
          WLP3 E 4 A_PlaySound("implord/familiarspawn")
          WLP3 F 6
          TNT1 A 0 A_Jump(85, 2)
          WLP3 G 8 A_SpawnItemEx("SummonedBatFamiliar", 0, 0, 32, 0, 0, 0, 0, SXF_SETMASTER)
		  Goto Death+6
		  WLP3 G 8 A_SpawnItemEx("DragonFamiliar", 0, 0, 32, 0, 0, 0, 0, SXF_SETMASTER)
          WLP3 H 350
		  WLP3 HHHHHHHHHHHHHHHHHHHH 1 A_FadeOut(0.05)
          Stop
     }
}

Actor RedSkullBall
{
    Radius 8
    Height 6
    Speed 15
    Damage 12
    Projectile
    RenderStyle Add
    Alpha 0.67
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    Decal PlasmaScorchLower
	+SHOOTABLE
	Health 20
    States
    {
    Spawn:
        WLP4 ABCB 4
        Loop
    Death:
        WLP4 DEFG 4
        Stop
    }
}

Actor WarlordMace
{
    +DOOMBOUNCE
    Speed 4
    States
    {
    Spawn:
        WLI2 ABCDEF 5
        WLI2 G 2100
		WLI2 G 0 ACS_ExecuteAlways(501, 0)
        Stop
    }
}

Actor WarlordShield
{
    +DOOMBOUNCE
    Speed 5
    States
    {
    Spawn:
        WLI1 ABCDEF 5
        WLI1 G 2100
		WLI1 G 0 ACS_ExecuteAlways(501, 0)
        Stop
    }
}

Actor SummonedBatFamiliar
{
    Health 10
    Radius 12
    Height 24
    Speed 14
    PainChance 200
    Monster
    +NOGRAVITY
    +FLOAT
    +NEVERRESPAWN
    SeeSound "batfam/idle"
    PainSound "batfam/pain"
    DeathSound "batfam/death"
    ActiveSound "batfam/idle"
    HitObituary "%o was scratched by a bat familiar"
    States
    {
    Spawn:
        BFAM ABCB 3 A_Look
        Loop
    See:
        BFAM ABCB 3 A_Chase
        Loop
    Melee:
        BFAM A 3 A_FaceTarget
        BFAM B 3 A_CustomMeleeAttack(Random(1,8)*1, "batfam/idle")
        BFAM CB 3 A_FaceTarget
        Goto See
    Pain:
        BFAM A 2
        BFAM A 2 A_Pain
        Goto See
    Death:
        TNT1 A 0 A_Die
        TNT1 A 0 A_NoBlocking 
        BFAM D 5 A_Scream
        BFAM E 5
        BFAM FDEF 5
        Goto Death+5
    Crash:
        TNT1 A 0 A_SetFloorClip
        BFAM G 6 A_PlaySound("batfam/crash")
        BFAM H 7
        BFAM I 8
        BFAM J 2100
		BFAM J 0 ACS_ExecuteAlways(501, 0)
        Stop
    }
}

Actor DragonFamiliar
{
    Health 25
    Radius 16
    Height 32
    Speed 10
    PainChance 200
	MaxTargetRange 78
    Monster
    +NOGRAVITY
    +FLOAT
    +STRIFEDAMAGE
    +NEVERRESPAWN
    SeeSound "dragonfam/see"
    PainSound "dragonfam/pain"
    DeathSound "dragonfam/death"
    ActiveSound "dragonfam/idle"
    Obituary "%o was cooked by a dragon familiar."
    States
    {
    Spawn:
        DFAM ABCD 10 A_Look
        Loop
    See:
        DFAM AABBCCDD 3 A_Chase
        Loop
    Missile:
        TNT1 A 0 A_JumpIfCloser(270, 1)
	    Goto See
	    TNT1 A 0 A_PlaySound("dragonfam/attack")
        DFAM E 2 Bright A_CPosRefire
        TNT1 A 0 A_CustomMissile("MiniFirePuffNoDamage", 12, 0, 0)
        DFAM E 2 Bright A_CPosRefire
        TNT1 A 0 A_CustomMissile("MiniFirePuff", 12, 0, Random(1,2))
        DFAM E 2 Bright A_CPosRefire
        TNT1 A 0 A_CustomMissile("MiniFirePuff", 12, 0, Random(-2,-1))
		TNT1 A 0 A_JumpIfCloser(270, 1)
        Goto See
		TNT1 A 0 A_Jump(48, "See")
		Goto Missile+2
    Pain:
        DFAM F 2
        DFAM F 2 A_Pain
        Goto See
    Death:
        TNT1 A 0 A_Die
        DFAM G 8 A_NoBlocking
        DFAM H 8 A_Scream
        DFAM I 6 A_SetFloorClip
        DFAM JKL 6
        DFAM M 2100
		DFAM M 0 ACS_ExecuteAlways(501, 0)
        Stop
    }
}

Actor MiniFirePuff
{
    Radius 6
    Height 10
    Speed 7
    Damage 1
    Projectile
    RenderStyle Add
    Alpha 0.67
    States
    {
    Spawn:
        DFFP ABCDEFGH 2 Bright
    Death:
        DFFP IJKLMNOPQR 2 Bright
        Stop
    }
}

Actor MiniFirePuffNoDamage : MiniFirePuff
{
    Damage 0
}